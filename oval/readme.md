[oval.prg](https://stigc.dk/c64/basic/?s=2RVHLboMwELz7K_bQAyQ03TUvg0okCm6DUhlkiGRf-v-f0TXO47YzHntmx4Sw2MlsMFzsW0JFnQrJ1HzVUOZSYYbtE1CGIkf4ni1MHaEsYJtBosyjZMqIZGu020TxUClqZFBRRQ09ZBhF5cunKrNF62uyjynw1VyK6nVeyyx5CGqZQm9GkAXuSiVqhMF1VGE7eM6FQiFY1ylsre8KFE1M03cYolQnqXJSsG56gRNKwTfAddxBMrijdYdhXpM-TQURgo-8P1p_WCcTeS7oZ15vX0AlCuJG9nWIl9ZmFEzC9A3uE0M6d86pCYOP2J-paWC7aANWbzdrBOeGfhxt7OoIwdB_qPSQs0-ELkDFIPH76vxJxEsvv_PW1e-Je5LqXll4LzYapr0m-RfkgriMu_E_)
```basic
10 PRINT CHR$(147)
20 POKE 53280,0:POKE 53281,0
30 FOR I=1024 TO 2023:POKE I,112:NEXT
40 FOR I=8192 TO 16191:POKE I,0:NEXT
50 POKE 53265,PEEK(53265) OR 32
60 POKE 53272,(PEEK(53272) AND 240) OR 8
70 CX=160:CY=100
80 RX=80:RY=40
90 FOR A=0 TO 6.28318 STEP .02
100 X=INT(CX+RX*COS(A))
110 Y=INT(CY+RY*SIN(A))
120 GOSUB 150
130 NEXT
140 END
150 IF X<0 OR X>319 OR Y<0 OR Y>199 THEN RETURN
160 ADDR=8192 + INT(Y/8)*320 + INT(X/8)*8 + (Y AND 7)
170 PLOT=7-(X AND 7)
180 POKE ADDR,PEEK(ADDR) OR 2^PLOT
190 RETURN
```
Visitors: click links to run code in online emulator

# [plasma with color cycling showing color codes](https://stigc.dk/c64/basic/?s=2dZDbboMwDIbv_RRWtQsobItzoMCWSW2attEoVECl9v1fZKZkGtO0myj-fx8-mwReuk-PRslS5KKegulP8V_oIqf6O2Ujc8lRH9oR3al_SkhvUpACe3_G3TU0exxC63Hc7hoPins_UlcPK7THhbt6Ay1wH844JNKYFIzAQ9djsALHDlmCQrAXUsstkjLbrLk4CeviRZaKyldpijSNLKsXbrcR2Prb-AePxJLv0myH8xaI5nH3OE7XU3SbI1UB8VLODklyW5cpbtv9RJRmLNyXAhAv6brGGiOrIruvtchu9eB6S0LqGAPxpsduuO5QCQFk4s25LnfzbbkiPwEVcYXp4bPO3O7uGv_NLenXmaqqAimXCOFneqidvXj_mbCbZgRSLTGk_g9Dmohx7KbTcK6KKOx733LF3g-gGCUc0H2QwfE0ydY9UwFKzvo7-w_9ZHWZOVAq5tsfwz1XoPTUfLz27Rc)
```basic
10 POKE 53280,0:POKE53281,0:POKE646,1:POKE 53272,21:PRINT CHR$(147)
20 REM BUILD SINE TABLE
30 PRINT "BUILDING SINE TABLE";
40 DIM S(255)
50 FOR I=0 TO 255
60 S(I)=INT(8+7*SIN(I*6.28318/256)):PRINT ".";
70 NEXT:PRINT CHR$(147)

100 REM BUILD PLASMA
110 FOR Y=0 TO 24:FOR X=0 TO 39
120 C=S((X*8) AND 255)+S((Y*8) AND 255)
130 COL=55296+Y*40+X:SCR=1024+Y*40+X
140 GOSUB 300
150 POKE COL,C:POKE SCR,H
160 NEXT:NEXT

200 REM CYCLE PLASMA
210 FOR I=0 TO 999
220 COL=55296+I:SCR=1024+I:C=PEEK(COL)+1
230 GOSUB 300
240 POKE COL,C:POKE SCR,H
250 NEXT:GOTO 200

300 REM SCREEN CODES
310 IF C>15 THEN C=C-16
320 IF C<10 THEN H=48+C
330 IF C>=10 THEN H=C-9
340 RETURN
```
# [plasma with color cycling - no color codes](https://stigc.dk/c64/basic/?s=2ZZHdbqNADIXv_RRWtNJCIO14hiFAy0rJZJqMSiACIiWXVdu7qJG6f-rb1wS6pdobND62jz8bErir7i1qJRMRiqwLujcN7ziKQ8o-SuYylBzVrmzRbOpvHkVzH6TA2m5xuXfFChtXWmwXy8KCYu9L6eSScuV6lJ3cQCRw5bbYeFJrH7TAu6pGlwtsK2QJYsE55-ds4SXBfMrNnpvGVzJRlFxLHfv-wDK5Yru5wNIe2v_wgMQYcFcsmu0CiPp5x2FelHXRoY9UCsRbmbzxvMM08XFRrjokP2DhOBaAeEtTFbnWMo2D4zQSwSFrTJ2TkNEQA_Gq66rZL1EJAaSHo3NfaPrjckeYUHbhPL-c3vDp9eEvnv88nIDiYbHuA3ztfhtzNIX92EbSl-ulaQpSjsHcJ5PLTL6z9t7jrB8QSDWGk9EXuG7SP8ANzmb4dH75_gsf3x5Pz8h4v59_gtQD4brqTsku_O97Sm6ztmSzlW1AMaW7Q_ODNLabTs7NjGJQstdvOX_RN3mUBAaUGurzz4SZpaCizrzd1-U7)
```basic
10 POKE 53280,0:POKE53281,0:POKE646,1:POKE 53272,21:PRINT CHR$(147)
20 REM BUILD SINE TABLE
30 PRINT "BUILDING SINE TABLE";
40 DIM S(255)
50 FOR I=0 TO 255
60 S(I)=INT(8+7*SIN(I*6.28318/256)):PRINT ".";
70 NEXT:PRINT CHR$(147)

100 REM BUILD PLASMA
110 FOR Y=0 TO 24:FOR X=0 TO 39
120 C=S((X*8) AND 255)+S((Y*8) AND 255)
130 COL=55296+Y*40+X:SCR=1024+Y*40+X
140 GOSUB 300
150 POKE COL,C:POKE SCR,81: REM only draw oval
160 NEXT:NEXT

200 REM CYCLE PLASMA
210 FOR I=0 TO 999
220 COL=55296+I:SCR=1024+I:C=PEEK(COL)+1
230 GOSUB 300
240 POKE COL,C:REM POKE SCR,H -- don't cycle values
250 NEXT:GOTO 200

300 REM SCREEN CODES
310 IF C>15 THEN C=C-16
320 IF C<10 THEN H=48+C
330 IF C>=10 THEN H=C-9
340 RETURN
```

# [simple plasma](https://stigc.dk/c64/basic/?s=2TVDRboMwDHz3V_hhD4R0W2xImlaiEk3piNZCBTyE__-RmUK1RorknC--85HBR__b2IK92Znjq6atdqXb0fExxG4K7fCRUblXAGxwaO54rscYcIxdg1N9vjVQGLzEO44ZW6ugNHjtB4yVwalHgcAa6UVVybTM630uX7OYuy_2Bflvtk4pcAa7Jk0AZN5VeNV53OrxXgOR2G6pkqvpGK_L4yQKOLVN9-wYIF44LBzWvHL4ncMLRxynKq0z0qk4rM20tDb782a_BBL7oRqzLOVei4bCursseykt4PwE-R8EklVkbDjRphmq8EkOaL9mjmS41HNeGp12np55o7V8cC8wAPk1DpyBDgZ_erEiwcBy_gA)
```basic
10 POKE53280,0:POKE53281,0:POKE646,1:PRINTCHR$(147)

20 REM BASIC SINE TABLE
30 DIM S(255)
40 FOR I=0 TO 255
50 S(I)=INT(8+7*SIN(I*6.28318/256))
60 NEXT

100 REM BASIC 2 SINE PLASMA
110 PH1=PH1+1:IF PH1>255 THEN PH1=0
120 PH2=PH2+2:IF PH2>255 THEN PH2=0
130 X=X+1:IF X>39 THEN X=0
140 FOR Y=0 TO 24
150 C=S((X*8+PH1) AND 255)+S((Y*8+PH2) AND 255)
160 IF C>15 THEN C=C-16
170 POKE 1024+Y*40+X,81:POKE 55296+Y*40+X,C
180 NEXT Y
190 GOTO 100
```
# [plasma with machine language stub](https://stigc.dk/c64/basic/?s=%3Chtml%3E%3Chead%3E%3Ctitle%3E415%20Unsupported%20Media%20Type%3C/title%3E%3C/head%3E%3Cbody%3E%3Ccenter%3E%3Ch1%3E415%20Unsupported%20Media%20Type%3C/h1%3E%3C/center%3E%3Chr%3E%3Ccenter%3Eopenresty/1.27.1.1%3C/center%3E%3C/body%3E%3C/html%3E%3C!--%20a%20padding%20to%20disable%20MSIE%20and%20Chrome%20friendly%20error%20page%20--%3E%3C!--%20a%20padding%20to%20disable%20MSIE%20and%20Chrome%20friendly%20error%20page%20--%3E%3C!--%20a%20padding%20to%20disable%20MSIE%20and%20Chrome%20friendly%20error%20page%20--%3E%3C!--%20a%20padding%20to%20disable%20MSIE%20and%20Chrome%20friendly%20error%20page%20--%3E%3C!--%20a%20padding%20to%20disable%20MSIE%20and%20Chrome%20friendly%20error%20page%20--%3E%3C!--%20a%20padding%20to%20disable%20MSIE%20and%20Chrome%20friendly%20error%20page%20--%3E)
```basic
10 POKE 53280,0:POKE 53281,0:POKE 646,1:PRINT CHR$(147)
20 DIM S(255):PRINT "GENERATING SINE TABLE..."
30 FOR I=0 TO 255:S(I)=INT(8+7*SIN(I*0.0245)):NEXT
40 PRINT "LOADING MACHINE CODE..."
50 C=0:FOR I=49152 TO 49201:READ D:POKE I,D:C=C+D:NEXT
60 IF C<>4775 THEN PRINT "CHECKSUM ERROR!":PRINT "GOT:";C;" NEED: 4775":END
70 PRINT "BUILDING PLASMA..."
80 FOR Y=0 TO 24:FOR X=0 TO 39
90 C=(S((X*8) AND 255)+S((Y*12) AND 255)) AND 15
100 POKE 1024+Y*40+X,81:POKE 55296+Y*40+X,C
110 NEXT:NEXT
120 REM PRINT "READY! PRESS SPACE TO START"
130 REM GET A$:IF A$<>" " THEN 130
200 SYS 49152:GOTO 200

500 DATA 162,0,189,0,216,24,105,1,41,15,157,0,216,189,0,217,24,105,1,41,15
510 DATA 157,0,217, 189,0,218,24,105,1,41,15,157,0,218,189,0,219,24,105,1
520 DATA 41,15,157,0,219,232,208,209,96
```

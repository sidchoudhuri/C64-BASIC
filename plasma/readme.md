# [simple plasma](https://stigc.dk/c64/basic/?s=2TVFdb4MwDHz3r7CqPRBgW2y-AhWVKKVrtBYq4CH8_z8yQ1qNSJGSu4vPvpDG5_DbZQkbHevqfabXOU_zmKrnaPu5vY0fAaWFAmCNY_fADUaPG3WsrsOIttY4D1iWBpK96oCHI6Qe6Ts3b9VZcxInDJB54txMtsXJ9h3OzfneQa7xYh84BZxlCgqNOwuBwGjhrKrFITBREcrTwIb5F5uEzDdnuVJQ6s0QgPTehb3P895MjwaIJIcb1bIjqux1vZzEAedb12-MBuJVw6LhiL2G9xpeNTK0q52v4U5J6Um3Uqlvf3m1nwLJ2G09BYELTSQeCpv-ss6lIgGXDeR_EEjikLLtiV6ebd1-Ug5U-E9E0pxGS5jqyMWGtogxy7jM32ALZHwcuABJMj-DtCLBwLr-AA)
```basic
10 POKE53280,0:POKE53281,0:POKE646,1:PRINTCHR$(147)

20 REM PRINT CHR$(18);:FOR I=0 TO 998
30 REM PRINT " ";
40 REM NEXT:POKE2023,32

50 REM BASIC SINE TABLE
60 DIM S(255)
70 FOR I=0 TO 255
80 S(I)=INT(8+7*SIN(I*6.28318/256))
90 NEXT

100 REM BASIC 2 SINE PLASMA
110 PH1=PH1+1:IF PH1>255 THEN PH1=0
120 PH2=PH2+2:IF PH2>255 THEN PH2=0
130 X=X+1:IF X>39 THEN X=0
140 FOR Y=0 TO 24
150 C=S((X*8+PH1) AND 255)+S((Y*8+PH2) AND 255)
160 IF C>15 THEN C=C-16
170 POKE 1024+Y*40+X,81:POKE 55296+Y*40+X,C
180 NEXT Y
190 GOTO 100
```
# [plasma with color cycling](https://stigc.dk/c64/basic/?s=2dZDbboMwDIbv_RRWtQsobItzoMCWSW2attEoVECl9v1fZKZkGtO0myj-fx8-mwReuk-PRslS5KKegulP8V_oIqf6O2Ujc8lRH9oR3al_SkhvUpACe3_G3TU0exxC63Hc7hoPins_UlcPK7THhbt6Ay1wH844JNKYFIzAQ9djsALHDlmCQrAXUsstkjLbrLk4CeviRZaKyldpijSNLKsXbrcR2Prb-AePxJLv0myH8xaI5nH3OE7XU3SbI1UB8VLODklyW5cpbtv9RJRmLNyXAhAv6brGGiOrIruvtchu9eB6S0LqGAPxpsduuO5QCQFk4s25LnfzbbkiPwEVcYXp4bPO3O7uGv_NLenXmaqqAimXCOFneqidvXj_mbCbZgRSLTGk_g9Dmohx7KbTcK6KKOx733LF3g-gGCUc0H2QwfE0ydY9UwFKzvo7-w_9ZHWZOVAq5tsfwz1XoPTUfLz27Rc)
```basic
10 POKE 53280,0:POKE53281,0:POKE646,1:POKE 53272,21:PRINT CHR$(147)
20 REM BUILD SINE TABLE
30 PRINT "BUILDING SINE TABLE";
40 DIM S(255)
50 FOR I=0 TO 255
60 S(I)=INT(8+7*SIN(I*6.28318/256)):PRINT ".";
70 NEXT:PRINT CHR$(147)

100 REM BUILD PLASMA
110 FOR Y=0 TO 24:FOR X=0 TO 39
120 C=S((X*8) AND 255)+S((Y*8) AND 255)
130 COL=55296+Y*40+X:SCR=1024+Y*40+X
140 GOSUB 300
150 POKE COL,C:POKE SCR,H
160 NEXT:NEXT

200 REM CYCLE PLASMA
210 FOR I=0 TO 999
220 COL=55296+I:SCR=1024+I:C=PEEK(COL)+1
230 GOSUB 300
240 POKE COL,C:POKE SCR,H
250 NEXT:GOTO 200

300 REM SCREEN CODES
310 IF C>15 THEN C=C-16
320 IF C<10 THEN H=48+C
330 IF C>=10 THEN H=C-9
340 RETURN
```
